# Tests for the cgi module.
#
# This file contains a collection of tests for one or more of the Tcl
# built-in commands.  Sourcing this file into Tcl runs the tests and
# generates output for errors.  No output means no errors were found.
#
# Copyright (c) 1998-2000 by Scriptics Corporation.
# All rights reserved.
#
# RCS: @(#) $Id: ncgi.test,v 1.4 2000/03/20 22:12:38 ericm Exp $

if {[lsearch [namespace children] ::tcltest] == -1} {
    package require tcltest
    namespace import ::tcltest::*
}

set ncgiFile [file join [file dirname [info script]] ncgi.tcl]
source $ncgiFile
package require ncgi 1.0

test ncgi-1.1 {ncgi::reset} {
    ncgi::reset
    info exist ncgi::query
} 0

test ncgi-1.1 {ncgi::reset} {
    ncgi::reset query=reset
    set ncgi::query
} query=reset

test ncgi-2.1 {ncgi::query fake query data} {
    ncgi::reset
    catch {unset env(REQUEST_METHOD)}
    ncgi::query "fake=query"
    set ncgi::query
} "fake=query"

test ncgi-2.2 {ncgi::query GET} {
    ncgi::reset
    set env(REQUEST_METHOD) GET
    set env(QUERY_STRING) name=value
    ncgi::query "fake=query"
    set ncgi::query
} "name=value"

test ncgi-2.3 {ncgi::query HEAD} {
    ncgi::reset
    set env(REQUEST_METHOD) HEAD
    catch {unset env(QUERY_STRING)}
    ncgi::query "fake=query"
    set ncgi::query
} ""

test ncgi-2.4 {ncgi::query POST} {
    ncgi::reset
    catch {unset env(QUERY_STRING)}
    set env(REQUEST_METHOD) POST
    set env(CONTENT_LENGTH) 10
    makeFile [format {
	source %s
	ncgi::query
	puts $ncgi::query
    } $ncgiFile] test1
    set f [open "|[list $::tcltest::tcltest test1]" r+]
    puts $f "name=value"
    flush $f
    gets $f line
    set line
} "name=value"

test ncgi-3.1 {ncgi::decode} {
    ncgi::decode abcdef0123
} abcdef0123

test ncgi-3.2 {ncgi::decode} {
    ncgi::decode {[abc]def$0123\x}
} {[abc]def$0123\x}

test ncgi-3.3 {ncgi::decode} {
    ncgi::decode {[a%25c]def$01%7E3\x%3D}
} {[a%c]def$01~3\x=}

test ncgi-4.1 {ncgi::encode} {
    ncgi::encode abcdef0123
} abcdef0123

test ncgi-4.2 {ncgi::encode} {
    ncgi::encode "\[abc\]def\$0123\\x"
} {%5Babc%5Ddef%240123%5Cx}

test ncgi-4.3 {ncgi::encode} {
    ncgi::encode {hello world}
} {hello+world}

test ncgi-4.4 {ncgi::encode} {
    ncgi::encode "hello\nworld\r\tbar"
} {hello%0D%0Aworld%0D%09bar}

test ncgi-5.1 {ncgi::list} {
    ncgi::reset
    catch {unset env(REQUEST_METHOD)}
    ncgi::list "name=hello+world&name2=%7ewelch"
} {name {hello world} name2 ~welch}

test ncgi-5.2 {ncgi::list} {
    ncgi::reset  "name=&name2"
    ncgi::list
} {name {} anonymous name2}

test ncgi-6.1 {ncgi::parse} {
    ncgi::reset
    catch {unset env(REQUEST_METHOD)}
    ncgi::parse "name=&name2"
} {name anonymous}

test ncgi-6.2 {ncgi::parse} {
    ncgi::reset "name=value&name=value2"
    ncgi::parse 
} {name}

test ncgi-7.1 {ncgi::input} {
    ncgi::reset
    catch {unset env(REQUEST_METHOD)}
    ncgi::input "name=value&name2=value2"
} {name name2}

test ncgi-7.2 {ncgi::input} {
    ncgi::reset "nameList=value1+stuff&nameList=value2+more"
    ncgi::input
    set ncgi::value(nameList)
} {{value1 stuff} {value2 more}}

test ncgi-7.3 {ncgi::input} {
    ncgi::reset "name=value&name=value2"
    catch {ncgi::input} err
    set err
} {Multiple definitions of name encountered in input. If you're trying to do this intentionally (such as with select), the variable must have a "List" suffix.}

test ncgi-8.1 {ncgi::value} {
    ncgi::reset "nameList=val+ue&nameList=value2"
    ncgi::input
    ncgi::value nameList
} {{val ue} value2}

test ncgi-8.2 {ncgi::value} {
    ncgi::reset "name=val+ue&name=value2"
    ncgi::parse
    ncgi::value name
} {val ue}

test ncgi-8.3 {ncgi::value} {
    ncgi::reset "name=val+ue&name=value2"
    ncgi::parse
    ncgi::value noname
} {}

test ncgi-9.1 {ncgi::valuelist} {
    ncgi::reset "name=val+ue&name=value2"
    ncgi::parse
    ncgi::valuelist name
} {{val ue} value2}

test ncgi-9.2 {ncgi::value} {
    ncgi::reset "name=val+ue&name=value2"
    ncgi::parse
    ncgi::value noname
} {}

test ncgi-10.1 {ncgi::import} {
    ncgi::reset "nameList=val+ue&nameList=value2"
    ncgi::input
    ncgi::import nameList
    set nameList
} {{val ue} value2}

test ncgi-10.2 {ncgi::import} {
    ncgi::reset "nameList=val+ue&nameList=value2"
    ncgi::input
    ncgi::import nameList myx
    set myx
} {{val ue} value2}

test ncgi-10.1 {ncgi::import} {
    ncgi::reset "nameList=val+ue&nameList=value2"
    ncgi::input
    ncgi::import noname
    set noname
} {}

set URL http://www.tcltk.com/index.html
test ncgi-11.1 {ncgi::redirect} {
    set env(REQUEST_URI) http://www.scriptics.com/cgi-bin/test.cgi
    set env(REQUEST_METHOD) GET
    set env(QUERY_STRING) {}
    set env(SERVER_NAME) www.scriptics.com
    set env(SERVER_PORT) 80
    makeFile [format {
	if {[catch {
	source %s
	ncgi::redirect %s
	} err]} {
	    puts $err
	}
    } $ncgiFile $URL] test1
    set f [open "|[list $::tcltest::tcltest test1]" r+]
    read $f
} "Content-Type: text/html\nLocation: $URL\n\nPlease go to <a href=\"$URL\">$URL</a>\n"

set URL /elsewhere/foo.html
set URL2 http://www.scriptics.com/elsewhere/foo.html
test ncgi-11.2 {ncgi::redirect} {
    set env(REQUEST_URI) http://www.scriptics.com/cgi-bin/test.cgi
    set env(REQUEST_METHOD) GET
    set env(QUERY_STRING) {}
    set env(SERVER_NAME) www.scriptics.com
    set env(SERVER_PORT) 80
    makeFile [format {
	if {[catch {
	source %s
	ncgi::redirect %s
	} err]} {
	    puts $err
	}
    } $ncgiFile $URL] test1
    set f [open "|[list $::tcltest::tcltest test1]" r+]
    read $f
} "Content-Type: text/html\nLocation: $URL2\n\nPlease go to <a href=\"$URL2\">$URL2</a>\n"

set URL foo.html
set URL2 http://www.scriptics.com/cgi-bin/foo.html
test ncgi-11.3 {ncgi::redirect} {
    set env(REQUEST_URI) http://www.scriptics.com/cgi-bin/test.cgi
    set env(REQUEST_METHOD) GET
    set env(QUERY_STRING) {}
    set env(SERVER_NAME) www.scriptics.com
    set env(SERVER_PORT) 80
    makeFile [format {
	if {[catch {
	source %s
	ncgi::redirect %s
	} err]} {
	    puts $err
	}
    } $ncgiFile $URL] test1
    set f [open "|[list $::tcltest::tcltest test1]" r+]
    read $f
} "Content-Type: text/html\nLocation: $URL2\n\nPlease go to <a href=\"$URL2\">$URL2</a>\n"

set URL foo.html
set URL2 http://www.scriptics.com/cgi-bin/foo.html
test ncgi-11.4 {ncgi::redirect} {
    set env(REQUEST_URI) /cgi-bin/test.cgi
    set env(REQUEST_METHOD) GET
    set env(QUERY_STRING) {}
    set env(SERVER_NAME) www.scriptics.com
    set env(SERVER_PORT) 80
    makeFile [format {
	if {[catch {
	source %s
	ncgi::redirect %s
	} err]} {
	    puts $err
	}
    } $ncgiFile $URL] test1
    set f [open "|[list $::tcltest::tcltest test1]" r+]
    read $f
} "Content-Type: text/html\nLocation: $URL2\n\nPlease go to <a href=\"$URL2\">$URL2</a>\n"

set URL foo.html
set URL2 http://www.scriptics.com:8000/cgi-bin/foo.html
test ncgi-11.5 {ncgi::redirect} {
    set env(REQUEST_URI) /cgi-bin/test.cgi
    set env(REQUEST_METHOD) GET
    set env(QUERY_STRING) {}
    set env(SERVER_NAME) www.scriptics.com
    set env(SERVER_PORT) 8000
    makeFile [format {
	if {[catch {
	source %s
	ncgi::redirect %s
	} err]} {
	    puts $err
	}
    } $ncgiFile $URL] test1
    set f [open "|[list $::tcltest::tcltest test1]" r+]
    read $f
} "Content-Type: text/html\nLocation: $URL2\n\nPlease go to <a href=\"$URL2\">$URL2</a>\n"

set URL foo.html
set URL2 https://www.scriptics.com/cgi-bin/foo.html
test ncgi-11.6 {ncgi::redirect} {
    set env(REQUEST_URI) /cgi-bin/test.cgi
    set env(REQUEST_METHOD) GET
    set env(QUERY_STRING) {}
    set env(SERVER_NAME) www.scriptics.com
    set env(SERVER_PORT) 443
    set env(HTTPS) "on"
    makeFile [format {
	if {[catch {
	source %s
	ncgi::redirect %s
	} err]} {
	    puts $err
	}
    } $ncgiFile $URL] test1
    set f [open "|[list $::tcltest::tcltest test1]" r+]
    read $f
} "Content-Type: text/html\nLocation: $URL2\n\nPlease go to <a href=\"$URL2\">$URL2</a>\n"

test ncgi-12.1 {ncgi::header} {
    makeFile [format {
	if {[catch {
	source %s
	ncgi::header
	} err]} {
	    puts $err
	}
    } $ncgiFile] test1
    set f [open "|[list $::tcltest::tcltest test1]" r+]
    read $f
} "Content-Type: text/html\n\n"

test ncgi-12.2 {ncgi::header} {
    makeFile [format {
	if {[catch {
	source %s
	ncgi::header text/plain
	} err]} {
	    puts $err
	}
    } $ncgiFile] test1
    set f [open "|[list $::tcltest::tcltest test1]" r+]
    read $f
} "Content-Type: text/plain\n\n"

test ncgi-12.3 {ncgi::header} {
    makeFile [format {
	if {[catch {
	source %s
	ncgi::header text/html X-Comment "This is a test"
	} err]} {
	    puts $err
	}
    } $ncgiFile] test1
    set f [open "|[list $::tcltest::tcltest test1]" r+]
    read $f
} "Content-Type: text/html\nX-Comment: This is a test\n\n"

::tcltest::cleanupTests
